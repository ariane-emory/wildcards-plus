// just in case it's a Pony-based model, hopefully shouldn't make a big, difference
// otherwise:
// score_9, score_8_up, score_7_up, score_6_up, \n

{|2 epic|3 dark} fantasy, masterpiece, ultra high resolution, 8k, detailed background,
wide shot,
\n

// set an 'm' or 'f' gender flag that will help guide the selection of picks later on.
{#m|#f}                

// character type flag, make 'wizards' 60% of the time:
{3 #wizard|2 #warrior} 

@weapon  := {2 spear|2 {|short|two-handed|bastard|curved} sword|axe|halbard|scimitar
            |falchion|kukri|rapier|sabre|cutlass|mace|dagger|torch|dirk|stiletto|blade
            |trident|harpoon|cleaver|hammer|warhammer|flail|whip|scourge|lash|morningstar
            }

// oh no, an accidental Easter egg! I completely forgot to document scalar string
// variables in the README.md file.
//
// TODO: document this properly!
//
// in the mean time, this just picks an item from @wepon and stashes it in a 
// variable called $weapon:

$weapon  := @weapon 

@character_adjectives := {?wizard  {#scary inhuman|#scary alien|#scary evil|#scary ominous
                                   |#scary sinister|#scary infernal|#alone lonesome
                                   |erudite|wise|ancient|immortal|antediluvian
                                   |?f seductive|?f beautiful|?f alluring|?m dashing
                                   |?f enchanting|?m !scary charming|?f beguiling
                                   |?f captivating|powerful|godlike|!scary holy}
                         |?warrior {!scary heroic|mighty|?m handsome|?m musclebound
                                   |#scary fearsome|resolute|2 proud|noble|tattooed
                                   |solitary #alone|athletic|steadfast|?m humongous|strong
                                   |?f beautiful|?m handsome|?f alluring|?m musclebound
                                   |?m !scary charming|!scary brave|!scary daring}
                         }

@roles := {?wizard  {{?m wizard|?f wizardress}|2 {?m sorcerer|?f sorceress}|2 {?m enchanter
                    |?f enchantress}|druid|alchemist|{?m priest|?f priestess}|adept|cultist
                    |vampire|lich|2 necromancer|?m warlock|?f witch|arcanist|sage|seer}
          |?warrior {2 warrior|!scary paladin|knight|barbarian|conqueror|rogue|2 gladiator
                    |?m swordsman|?f swordswoman|?f amazon|dervish|blademaster|deulist
                    |{?m hunter|?f huntress}|?f valkyrie|mercenary|#scary {brigand|bandit}
                    |!scary hero|#scary raider|myrmidon|cataphract|hoplite|legionnaire}
          }

// The gender flag and the guards here will ensure that only one alternative remain
// available, forcing the script to pick that alternative. we'll deliberately use that to
// pick appropriately gendered pronouns for our character:

@pro_3rd_object  := {?f her |?m him | ?n it  }
@pro_3rd_subject := {?f she |?m he  | ?n it  }
@pro_pos_adj     := {?f her |?m his | ?n its }
@pro_pos         := {?f hers|?m his | ?n its }
// @pro_pos_rand    := {4 @pro_pos_adj|3 a|1 their}
@pro_pos_rand    := {5 @pro_pos_adj|4 a|1 their}

@video_game_descriptor := {highly anticipated|blockbuster|award winning}
@video_game_origin     := {|3 from {Japan's FROM Software|2 Hidetaka Miyazaki}|, the
                            sequel to Bloodborne}
{3 A {|oil} painting for the cover of a fantasy novel published in the
 {3 1970s|2 1980s|1990s|early 2000s}
 , in the style of
 {3 Julie Bell|2 Boris Vallejo|2 Frank Frazetta|3 Ian Miller|3 Zdzislaw Beksinski
 |4 Gerald Brom|3 {Yoshiaki Kawajiri|the anime movie \'Ninja Scroll'}
 |2 an 'Ace double' novel|Tor Books|an Omni Magazine cover|a Weird Tales magazine cover
 |a Heavy Metal magazine cover}
|A screenshot from a {cinematic|cutscene} {3|before the first boss fight} in a
 @video_game_descriptor video sgame @video_game_origin
|A promotional poster for a @video_game_descriptor video game @video_game_origin
}

, {which depicts|depicting}

@world_adjective := {ancient|primevil|medieval|Atlantean|Victorian|Mesopotamian|forgotten
                    |primitive|frightening|hellish|alien|{haunted|{ghost|demon}-haunted}
                    |ruined|abandoned|lonely|chaotic|crowded|mythical|fantastical
                    }
                    
{|2 a scene in a @world_adjective world {showing|featuring|focused on|centered on}}

a

@2-3&character_adjectives @roles

@wizard_clothing_adjs := {swirling|cowled|hooded|shadowy|multicolored|oily|many-layered
                         |ornate|silken|brightly colored|occult|embroidered}
@pluralize_clothing   := {!used_pro <s}

// Clothing: only reference any clothing half the time, because of the empty alternative at the start:
{|{clad in|wearing|dressed in} 
 {?wizard  {|@pro_pos_rand #used_pro}
           @0-2&wizard_clothing_adjs
           {3 robe @pluralize_clothing|garb|raiment|vestments|?f dress|?f gown},
 |?warrior {3 {|battered|blood-smeared|2 leather|spiked|chainmail|rusted|ornate|golden}
           {3 armor|{|@pro_pos_rand #used_pro} helmet|pauldrons|wargear|gauntlets}
           |{hides|furs|leather}
           }
 }
}

{2|{ in the {|early} morning|2 at mid-day|3 in mid-evening|3 at midnight|3 in the dead of night
   |3 on a {moonlit|starlit} night} , }

// A weapon or item: don't mention a weapon/item one time in six, because of the empty alternative at the
// start:
{|5 {presenting|brandishing|holding
    |?warrior wielding|?warrior swinging|?wizard with @pro_pos_adj hands held over
    |?wizard cradling |?wizard waving|?wizard gesturing with}

    @pro_pos_rand

    {?wizard  {jewel-encrusted|enchanted|magical|glowing|ancient|3}
              {2 staff|2 wand|scroll|tome|map|crystal ball|amulet|artifact
              |skull|spell book|dagger|2 candle|brazier|bell
              |2 {| glowing|?scary fiery} orb
              |potion|cauldron
              }
    |?warrior {2 bloody|2 ornate|bejeweled|fiery|}
              $weapon
    }
    // {|,}
}

{|while}

{|#outdoors}
{|?outdoors #mounted}

{?mounted {sitting on|mounted on|riding on|astride|leading}
          @pro_pos_rand
          {|mighty|infernal|#scary terrifying|majestic|?warrior armored}
          {3 beast|wolf|2 lizard|reptile|snake|serpent|insect|2 steed|2 destrier|2 horse|2 dragon}
          ,
}

{!outdoors {|standing|crouching|lurking
            |?wizard {standing|seated} {at|behind|before} @pro_pos_rand
                     {lectern|obelisk|monolith|podium|altar|desk|grimoire|idol
                     |crystal ball|{|bubbling} alchemical equipment}
            |3 {lounging on|laying on|sitting on} @pro_pos_rand {throne|sofa|couch|chair} } }

in {3|the heart of|the depths of} a

{!outdoors {|shadowy|cyclopean|smoky|hidden|crumbling|abandoned
            |ruined|foggy|dimly lit|dusty|{candle|torch} <lit| eerily lit}
           {temple|shrine|palace|crypt|cave|tavern|church|lair|cave|cavern|tomb
           |{|arcane|wizard's} library|castle|throne room|tunnel|dungeon|tower}
}

{?outdoors {|ancient|shadowy|smoky|foggy|dimly lit|stormy|wintry|icy|snowy
            |rainy|starlit|moonlit|secluded|lonely|burnt|lightning-{lit|blasted}}
           {swamp|forest|desert|wasteland|ruin|alleyway|canal|field|battlefield
           |slum|courtyard|garden|crater|canyon|thicket|scrubland|boat|ship}
}

{,|and}

@look_aim := {past|towards|2 {|up|down} {at|towards}}

// Expression/looking at:
{2 looking @look_aim
|#angry glaring @look_aim
|staring @look_aim
|gazing @look_aim
|beckoning {|@look_aim}
|inviting // no @look_aim on this one!
|leering @look_aim
|#scary glowering @look_aim
|2 !scary smiling @look_aim
|grinning @look_aim
|scowling @look_aim
}

the viewer

{2 ?scary menacingly|pridefully|cunningly|drunkenly|!scary !angry jovially|!scary !angry fondly
|!angry gleefully|blankly|despondantly|madly|?scary angrily|angrily|mockingly|?f seductively
|?m roguishly|?m rakishly|2 ?scary threateningly|!scary victoriously|3}

@minion_adjectives := { ?wizard   {?scary {demonic #demonic|fearsome|slavering|3 undead|eldrich
                                          |loathsome|squamous|chtonian|repulsive|chitinous|ghoulish} 
                                  |       {2 arcane|cowering|bewitched|hypnotized|2 ensorceled|reptilian
                                          |otherwordldly|mysterious|exotic|obedient|worshipping|2 dancing
                                          |swarming}
                                  }
                      |  ?warrior {fellow|loyal|brave|steadfast|armored|mighty
                                  |heavily armed|raucous|warlike|belligerant}
                      }

// unless flagged as #alone, warriors always have #many_minions, but wizards may have a single minion
// sometimes: 
{?warrior #many_minions|?wizard {|3 #many_minions}}
@pluralize_minions := { ?many_minions <s }

// Companions: don't always mention the companions, because of the empty alternative at the start 
{|!alone {2 ,|while}
         {accompanied by|?many_minions surrounded by|?many_minions encircled by
         |guarded by|leading|commanding|directing|guiding|rallying|alongside}
         @pro_pos_rand
         {?many_minions {horde|host|?warrior {|2 motley|3 merry} {band|crew}|?warrior gang|?warrior squad
                        |army|?wizard coterie|?wizard circle|?wizard coven|?wizard variety}
                        of
         }
         {?warrior @0-2&minion_adjectives
                   {soldiers|warriors|companions|men at arms|compatriots}
         |?wizard  {@1-3&minion_adjectives|@2minion_adjectives}
                   {familiar|2 minion|slave|cultist|devil|!demonic demon|spirit|apprentice|followers}
                   @pluralize_minions
         }
}

{4 
|and basking in {|2 @pro_pos_adj} {worship|glory}
|and {reveling in|enjoying} the {chaos|destruction|din|silence|darkness|?alone solitude|?warrior violence} 
}

.
