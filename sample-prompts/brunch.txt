@formats   := {3 {2|grilled|toasted} sandwich #used_bread|banh mi #used_bread|panini #used_bread
              |hoagie #used_bread|wrap|3 plate #pluralize #plate|burger|taco
              |omelette #used_eggs|scramble #used_eggs}
@pluralize := {?pluralize <s}
@toppings  := {
              4 !#cheese
                {4 
                |3 mozarella
                |2 cheddar
                |2 provolone
                |gouda
                |parmesan
                |2 Swiss
                |Monterey jack
                |3 American
                |Colby {|jack}
                |Oaxaca}
                cheese
              |3 !#used_eggs egg @pluralize
              |!used_eggs !plate mayonnaise
              |!used_eggs aioli
              |horseradish

              // Meats:
              |2 bacon
              |2 {|fried|grilled} chicken
              |2 Spam
              |brisket
              |ham
              |corned beef
              |crab
              |lobster
              |oysters
              |pastrami
              |sausage @pluralize
              |steak
              |{3|2 grilled|ground|roast} beef
              |{|garlic|fried|coconut} shrimp

              // Veggies:
              |2 tomato @pluralize
              |2 mushrooms
              |cucumber
              |kimchi
              |{4|jalapeno|banana|hot|chili} peppers
              |pickles
              |!plate capers
              |3 {4|pickled|red|green} onion
              |sauerkraut

              // Sauces:
              |!#sauced !plate ketchup
              |!#sauced sauce
              |!#sauced 2 gochujang 
              |!#sauced hot sauce
              |!#sauced satay sauce
              |!#sauced teriyaki sauce 
              |!#sauced !plate !used_eggs {|Dijon} mustard 
              |!#sauced 4 gravy              
              }
@sides     := {!#used_potatoes 3 {|French} fries
              |!#noodles 3 {|chow mein|rice|mi goreng|pad see ew|instant|Indo Mis} noodles
              |!#noodles pasta
              |!#used_bread a bagel
              |!#used_potatoes hash browns|macaroni
              |!#used_potatoes potato chips
              |!#used_potatoes poutine
              |!#used_potatoes tater tots|pierogies
              |2 cabbage rolls
              |2 vegetables|2 !#used_potatoes potatoes
              |3 !#used_bread {|2 buttered} {toast|bread}
              |5 !#used_bread a bialy
              |{4|baked|refried|black} beans
              |!#soup 2 {4|2 chicken|wonton} soup
              |!#soup borscht
              |!#soup chowder
              |!#soup stew
              |!#salad coleslaw
              |!#salad 2 {|a} salad
              |dumplings
              |pancakes
              |!#rice 2 {2|fried} rice
              |!#rice pilaf}
@relation  := {served with|paired with|accompanied by}
@relation2 := {with|and|sitting {beside|next to}|alongside}
@drink     := {glass of {milk|orange juice}
              |{mug|cup} of {{2|green} tea|coffee}
              |{|'Rockstar' brand|'Monster' brand|'Red Bull' brand} energy drink
              |{glass|mug|can|bottle} of beer}

A high-definition, 25-megapixel photograph of a
{|2 delicious{|<-looking}
|amazing
|Michelin star
|award-winning
|appetizing
|2 fresh {|<ly made}
|savory
|tasty
|2 mouth-watering
} @formats with @3-4&toppings @relation @2&sides @relation2 a single @drink.
