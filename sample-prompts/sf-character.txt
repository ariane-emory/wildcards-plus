// =======================================================================================
// random sci-fi characters
// =======================================================================================

@pro_pos_rand := {2 @pro_pos_adj|a}

{3 #female|2 #male|#neuter}
{#planet|#ship|#moon|#station}

@ship_term     := {starship|spacecraft|space ship|shuttle}
@ship_late_adj := {space|interstellar}
@ship_term2    := {starship|spacecraft|@ship_late_adj ship|shuttle|@ship_late_adj frigate
                  |@ship_late_adj battleship|@ship_late_adj destroyer
                  |{|@ship_late_adj} merchant vessel
                  |{|@ship_late_adj} vessel}

// ---------------------------------------------------------------------------------------
// L2 places:
// ---------------------------------------------------------------------------------------
@l2_place     := {on a @l2_place_adj {?ship @ship_term2|?station space station
                 |?planet planet|?planet world|?moon moon}}
@l2_place_adj := 
{
|strange
|exotic
|crowded
|busy
|remote
|secluded
|run-down
|!#advanced advanced
|primitive
|backwater
|#highly-detailed highly-detailed
|#greebled {|highly} greebled
}
@l2_orbit_obj_adj   := {dim|distant|huge|luminous|shining}
@l2_orbit_obj       := {2 !planet {|ringed} planet|!planet !moon ship
                       |!planet !moon space station|!planet !moon asteroid
                       |!planet comet|2 {|2 yellow|2 red|green} star|black hole
                       |!planet !moon satellite}
@l2_orbit_phrase    := {@l2_orbit_obj_adj @l2_orbit_obj}
@l2_place_phrase    := {@l2_place {|orbitting a @l2_orbit_phrase}}

// ---------------------------------------------------------------------------------------
// places:
// ---------------------------------------------------------------------------------------
@place_to_be_in := 
{2 #indoors  {2 crew cabin 
             |!planet !moon 2 cockpit
             |lounge
             |bar
             |2 market
             |laboratory
             |!ship aerodyne
             |!ship {sky|hover|grav} {<car|transport|vehicle}
             |!ship {ground|all-terrain} {<car|transport|vehicle}             
             |air/raft
             |equipment room
             |control room
             |!planet !moon engine room
             |security room
             |corridor
             |?ship docking bay
             |?ship cargo bay
             |?ship @ship_term<'s bridge
             |?station docking bay
             |?station cargo bay
             |{access|maintenance} tunnel
             }
|3 !ship !station #outdoors {alley
                            |{|hive city|megacity} street
                            |crater
                            }
}
@place_to_be_at :=
{{ware|store} <house
|{2|holographic} computer
|colony
|holographic projector
|!ship !station launch pad
|!planet !moon helm
|!planet !moon {|navigation|control|sensor|computer} console
}
@place_phrase := {in {a|the} @place_to_be_in|at {a|the} @place_to_be_at}

// ---------------------------------------------------------------------------------------
// character adjectives:
// ---------------------------------------------------------------------------------------
@char_early_adj :=
{adventurous
|androgynous
|mysterious
|solitary #solo
|inscrutable
|independant #solo
|daring
|resolute
|brave
|cunning
|inhuman
|intimidating
|exotic
|otherworldly
|athletic
|muscular
|youthful
|beautiful
}
@char_late_adj :=
{3
|alien #alien
|android
|cyborg #cyborg
|human #human
}

// ---------------------------------------------------------------------------------------
// weapons:
// ---------------------------------------------------------------------------------------
@weapon_phrase           := {@weapon_verb @pro_pos_rand {2 !melee @ranged_weapon|@melee_weapon}}
@weapon_verb             := {3 holding|wielding|brandishing|displaying|presenting}
@melee_weapon            := {{vibro|phase|energy|monomolecular|pulse} {sword|blade|knife|dagger|baton}}
@ranged_weapon_early_adj := {|automatic|primitive|!#high-tech high-tech|!#advanced advanced|alien
                            |#highly-detailed highly-detailed|#greebled {|highly} greebled}
@ranged_weapon_late_adj  := {|laser|plasma|gauss|smart|pulse|combat|high-velocity|ion}
@ranged_weapon           := {@ranged_weapon_early_adj
                             @ranged_weapon_late_adj
                             {3 pistol|2 {|sniper|hunting} rifle|2 submachinegun
                             |{grenade|rocket} launcher|stunner|cannon|disintegrator}
                            }                

// ---------------------------------------------------------------------------------------
// tool:
// ---------------------------------------------------------------------------------------
@tool_adj    := {!#advanced advanced|intricate|alien|!#high-tech high-tech|holographic#
                |#highly-detailed highly-detailed|#greebled {|highly} greebled}
@tool_phrase := {holding @pro_pos_rand @tool_adj @tool}
@tool :=
{hydrospanner
|2 probe
|2 scanner
|2 toolkit
|equipment
|instruments
|pair of binoculars
|telescope
|plasma cutter
|3 vacuum suit's helmet under one arm
|optical device
|tricorder
|cyberdeck
|2 datapad
|2 communicator
|1 radio
}

// ---------------------------------------------------------------------------------------
// inventory flags:
// ---------------------------------------------------------------------------------------
@maybe_no_equipment := {|2 #tool|#weapon}
@tool_or_weapon     := {#tool|#weapon}
@likely_weapon      := {#tool|2 #weapon}
@likely_tool        := {2 #tool|#weapon}

// ---------------------------------------------------------------------------------------
// role:
// ---------------------------------------------------------------------------------------
@role :=
{#weapon mercenary 
|{3|hyperdrive|@ship_term} technician        @likely_tool
|communications officer                      @maybe_no_equipment
|scientist                                   #tool
|explorer                                    @likely_tool
|scout                                       @tool_or_weapon
|{2|interstellar} bounty hunter              #weapon
|soldier                                     #weapon
|prize fighter                               #weapon #melee
|assassin                                    @likely_weapon
|medic                                       #tool
|worker                                      #tool
|{space|star|interstellar} {trader|merchant} @maybe_no_equipment
|{space|star} marine                         #weapon
|@ship_term captain                          @maybe_no_equipment
|@ship_term pilot                            @maybe_no_equipment
|{|@ship_term} {navigator|astrogator}        @maybe_no_equipment
}

// ---------------------------------------------------------------------------------------
// clothing:
// ---------------------------------------------------------------------------------------
@clothing_sentence := {@^pro_3rd_subj is @clothing_verb @clothing_type.}
@clothing_verb     := {2 wearing|clad in|dressed in}
@clothing_adj      := {2|1 futuristic|!#advanced advanced|tattered|dusty|millitary|!#high-tech high-tech
                      |{|starship|space ship|spacecraft} crew member's|#highly-detailed highly-detailed
                      |#greebled {|highly} greebled}
@clothing_type     := {?weapon {|@pro_pos_adj} @clothing_adj {|combat|power|space} armor
                      |@pro_pos_rand @clothing_adj flight suit
                      |@pro_pos_rand @clothing_adj body suit
                      |@pro_pos_rand @clothing_adj uniform
                      |@pro_pos_rand @clothing_adj {|helmetless} {space|vacuum|hazardous environment} suit
                      |@pro_pos_rand @clothing_adj exoskeleton}
 
// ---------------------------------------------------------------------------------------
// cyborg feature:
// ---------------------------------------------------------------------------------------
@cyborg_feature_sentence := {?cyborg
  @^pro_3rd_subj has
  {one mechanical arm
  |mechanical arms
  |mechanical arms
  |one cybernetic eye
  |cybernetic eyes
  }
  .
}

// ---------------------------------------------------------------------------------------
// ailen feature:
// ---------------------------------------------------------------------------------------
@alien_feature_sentence := {?alien
  @^pro_3rd_subj has
  {an insectoid appearance
  |digitigrade legs
  |gills on her throat
  |razor sharp claws
  |chitinous armored plates on @pro_pos_adj skin
  |scaled skin
  |a prehensile tail
  |a reptilian appearance
  }
  .
}

// ---------------------------------------------------------------------------------------
// sky:
// ---------------------------------------------------------------------------------------
@sky_sentence := {?outdoors {2
|{Two|Three} moons can be seen in the sky above.
|The planet's ring can be seen arching over in the sky above.
|A descending @ship_term2 can be seen in the sky above.
|Falling meteors can be seen in the sky above.
|An orbiting space station can be seen in the sky above.
|Strangely coloured clouds can be seen in the sky above.
|Strange lights can be seen in the sky above.
|Flocks of alien birds can be seen in the sky above.
|Flying vehicles can be seen in the sky above.
|A {|huge|gigantic} {2 |2 red|2 yellow|green} {2 sun|star going {nova|supernova}} can be seen in the sky above.
}}

// =======================================================================================
@colour_modifiers := {warm color pallete|warm lighting
                     |?outdoors natural lighting|?outdoors golden hour}

@aris_defaults, science fiction, {|futuristic,} hyperrealistic, ethereon, infinite focus,
@0-2,colour_modifiers
.

\n\n

A @0-2&char_early_adj @char_late_adj @role @place_phrase {|@l2_place_phrase}
{|5 ?weapon @weapon_phrase}
{|5 ?tool   @tool_phrase}
.

{|5 @clothing_sentence}
{|5 @cyborg_feature_sentence}
{|5 @alien_feature_sentence}
@sky_sentence
